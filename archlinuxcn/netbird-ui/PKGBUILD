# Maintainer: Integral <integral@member.fsf.org>
# Contributor: tarball <bootctl@gmail.com>

pkgname=netbird-ui
_pkgname=${pkgname%-ui}
pkgver=0.28.9
pkgrel=1
pkgdesc="Official GUI for the Netbird client"
url="https://${_pkgname}.io"
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64' 'riscv64')
license=('BSD-3-Clause')
depends=(
	"at-spi2-core"
	"ayatana-ido"
	"cairo"
	"gdk-pixbuf2"
	"glib2"
	"gtk3"
	"harfbuzz"
	"libayatana-appindicator"
	"libayatana-indicator"
	"libdbusmenu-glib"
	"libglvnd"
	"libx11"
	"libxcursor"
	"libxi"
	"libxinerama"
	"libxrandr"
	"libxxf86vm"
	"${_pkgname}"
	"pango"
	"zlib"
)
makedepends=('go')
source=(
	"${pkgname}-${pkgver}.tar.gz::https://github.com/netbirdio/${_pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
)
sha256sums=('c3cc721330cbe8341665e982f8e7c77e0a37f0fedaee07f44fc78b5e200eb1c3')

prepare() {
	cd "${_pkgname}-${pkgver}/"
	mkdir -p build
	go mod download
}

build() {
	cd "${_pkgname}-${pkgver}/client/ui/"

	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

	go build \
		-o "../../build/${pkgname}" \
		-trimpath \
		-buildmode=pie \
		-mod=readonly \
		-modcacherw \
		-ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X github.com/netbirdio/${_pkgname}/version.version=${pkgver}" \
		.
}

package() {
	cd "${_pkgname}-${pkgver}/"
	install -Dm755 "build/${pkgname}" -t "${pkgdir}/usr/bin/"
	install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

	cd client/ui
	install -Dm644 netbird.desktop -t "${pkgdir}/usr/share/applications/"
	install -Dm644 netbird.ico -t "${pkgdir}/usr/share/icons/"
}
